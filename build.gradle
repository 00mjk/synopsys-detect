buildscript {
    repositories { mavenCentral() }

    dependencies {
        classpath('org.springframework.boot:spring-boot-gradle-plugin:1.5.2.RELEASE')
        classpath "org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.8.1"
    }
}

apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'org.springframework.boot'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'

tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }
tasks.withType(GroovyCompile) { options.encoding = 'UTF-8' }

group = 'com.blackducksoftware.integration'

// When updating the version, please keep latestReleaseVersion and latestSnapshotVersion updated as well
// these values will drive the update to the shell script so that consumers of the shell script will get
// the correct version of the final hub-packman jar. ekerwin @ 2017-05-19
version = '0.0.2-SNAPSHOT'
def latestReleaseVersion = '0.0.1'
def latestSnapshotVersion = '0.0.2-SNAPSHOT'

sourceCompatibility = 1.7
targetCompatibility = 1.7

build {
    doLast {
        def shellScriptContents = new File("${projectDir}/src/main/resources/hub-packman-sh").getText('UTF-8')
        shellScriptContents = shellScriptContents.replace('**BUILD_SCRIPT_UPDATE_LATEST**', "hub-packman-${latestReleaseVersion}.jar")
        shellScriptContents = shellScriptContents.replace('**BUILD_SCRIPT_UPDATE_SNAPSHOT**', "hub-packman-${latestSnapshotVersion}.jar")
        def shellScriptFile = new File("${buildDir}/hub-packman.sh")
        shellScriptFile.delete()
        shellScriptFile << shellScriptContents
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {
    compile 'com.blackducksoftware.integration:hub-common:11.0.0'
    compile 'org.springframework.boot:spring-boot-starter'
    compile 'org.apache.maven.shared:maven-invoker:3.0.0'
    compile 'com.esotericsoftware.yamlbeans:yamlbeans:1.11'
    compile 'org.codehaus.groovy:groovy-all:2.4.11'

    testCompile 'org.springframework.boot:spring-boot-starter-test'
}

springBoot { mainClass = 'com.blackducksoftware.integration.hub.packman.Application' }

jacocoTestReport {
    reports { xml.enabled = true }
}

