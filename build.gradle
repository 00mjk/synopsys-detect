buildscript {
    repositories { mavenCentral() }

    dependencies {
        classpath('org.springframework.boot:spring-boot-gradle-plugin:1.5.2.RELEASE')
        classpath "org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.8.1"
    }
}

apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'org.springframework.boot'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'

tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }
tasks.withType(GroovyCompile) { options.encoding = 'UTF-8' }

group = 'com.blackducksoftware.integration'

version = '0.0.3-SNAPSHOT'
// the override.snaphot property should be set to true for a CI build so
// that the version is omitted from a snapshot build artifact
if (version.contains('SNAPSHOT') && "true" == project.findProperty('override.snapshot')) {
    version = 'latest-SNAPSHOT'
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

build {
    doLast {
        def shellScriptContents = new File("${projectDir}/src/main/resources/hub-packman-sh").getText('UTF-8')
        if (!version.contains('SNAPSHOT')) {
            shellScriptContents = shellScriptContents.replaceFirst("PACKMAN_LATEST_RELEASE_VERSION=\\S*\n", "PACKMAN_LATEST_RELEASE_VERSION=\"${version}\"\n")
        }
        def shellScriptFile = new File("${buildDir}/hub-packman.sh")
        shellScriptFile.delete()
        shellScriptFile << shellScriptContents
        shellScriptFile.setExecutable(true)
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {
    compile 'com.blackducksoftware.integration:hub-common:12.0.0-SNAPSHOT'
    compile 'org.springframework.boot:spring-boot-starter'
    compile 'org.apache.maven.shared:maven-invoker:3.0.0'
    compile 'com.esotericsoftware.yamlbeans:yamlbeans:1.11'
    compile 'org.codehaus.groovy:groovy-all:2.4.11'

    testCompile 'org.springframework.boot:spring-boot-starter-test'
}

springBoot { mainClass = 'com.blackducksoftware.integration.hub.packman.Application' }

jacocoTestReport {
    reports { xml.enabled = true }
}
